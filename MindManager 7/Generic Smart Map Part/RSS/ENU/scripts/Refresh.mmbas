'#Reference {F5078F18-C551-11D3-89B9-0000F81FE221}#6.0#0#C:\WINDOWS\system32\msxml6.dll#Microsoft XML, v6.0
'#Uses "language.MMBas" ' language file placed by installation script
Option Explicit

Private Declare Function URLDownloadToFile Lib "urlmon" Alias _
	"URLDownloadToFileA" (ByVal pCaller As Long, ByVal szURL As String, ByVal _
		szFileName As String, ByVal dwReserved As Long, ByVal lpfnCB As Long) As Long

Sub Main

	Dim aTopic As Topic

	Set aTopic = GetPrimarySelection()

	Dim RssURL As String

	RssURL = GetRSSURL(aTopic)

	Dim imageLocation As String

	imageLocation = GetImageLocation(aTopic)

	Dim bold

	bold = aTopic.Font.Bold

	Dim fillcolor

	fillcolor = aTopic.FillColor.Value

	Dim textcolor
	textcolor = aTopic.TextColor.Value

	If RssURL="" Then
		Exit Sub
	End If

	'On Error Resume Next

	Dim xsldoc
	Set xsldoc = CreateObject("Msxml2.FreeThreadedDOMDocument.6.0")
	xsldoc.async = False
	xsldoc.resolveExternals = True

    Dim Path As String

    Path = MacroDir + "\FromRss.xslt"

 	If Not xsldoc.Load(Path) Then
 		MsgBox(locCouldNotLoadXslt + vbCrLf + Path)
 		Exit Sub
 	End If

	Dim xmldoc
	Set xmldoc = CreateObject("Msxml2.FreeThreadedDOMDocument.6.0")

	xmldoc.async = False
	xmldoc.validateOnParse = False
	xmldoc.resolveExternals = False

	Dim actualRssUrl As String
	actualRssUrl = Replace(RssURL, "feed://", "http://")
	actualRssUrl = Replace(actualRssUrl, "feed:\\", "http://")

    If Not xmldoc.Load(actualRssUrl) Then
 		MsgBox(locCouldNotLoadRss + vbCrLf + RssURL)
 		Exit Sub
 	End If

 	Dim imageURL As String
 	imageURL = GetImageURL(xmldoc)
	imageURL = DownloadImage(imageURL)

	Dim xml As String
	xml = Transform(xmldoc, xsldoc, imageLocation)
	Debug.Print(xml)

	If InStr(xml, "Topic") = 0 Then
		MsgBox(RssURL + vbCrLf + ERR_INVALID_RSS_NEWS)
		Exit Sub
	End If

	aTopic.Xml = xml
	aTopic.Attributes("http://schemas.mindjet.com/MindManager/GSMP/2003").SetAttributeValue("RssUrl", RssURL)
	aTopic.Attributes("http://schemas.mindjet.com/MindManager/GSMP/2003").SetAttributeValue("ImageLocation", imageLocation)
	aTopic.Font.Bold = bold
	aTopic.TextColor.Value = textcolor
	aTopic.FillColor.Value = fillcolor

	If Not imageURL="" Then
		Dim aImage As Image

		Set aImage = aTopic.CreateImage(imageURL)
		
		On Error Resume Next
		Kill(imageURL)
	End If

	Exit Sub
End Sub

Function GetImageURL(xmldoc) As String
 	Dim imageURL

 	Set imageURL = xmldoc.selectSingleNode("/rss/channel/image/url")

	If imageURL Is Nothing Then
		xmldoc.setProperty("SelectionNamespaces", "xmlns:rdf='http://www.w3.org/1999/02/22-rdf-syntax-ns#' xmlns:rss='http://purl.org/rss/1.0/'")
 		Set imageURL = xmldoc.selectSingleNode("rdf:RDF/rss:channel/rss:image/rss:url")
	End If

 	If imageURL Is Nothing Then
 		Set imageURL = xmldoc.selectSingleNode("rdf:RDF/rss:image/rss:url")
 	End If

 	If imageURL Is Nothing Then
		GetImageURL = ""
		Exit Function
 	End If

 	GetImageURL = imageURL.Text
End Function


' Get the primary selection topic
Function GetPrimarySelection()
	Dim aTopic As Topic

	Dim aSelection As Selection

	Set aSelection = ActiveDocument.Selection

	Set GetPrimarySelection = aSelection.PrimaryTopic
End Function


' Get the RSS URL of the primary selection
Function GetRSSURL(aTopic)
	If (aTopic Is Nothing) Then
		GetRSSURL = ""
		Exit Function
	End If

    If Not(aTopic.HasAttributesNamespace("http://schemas.mindjet.com/MindManager/GSMP/2003")) Then
		GetRSSURL = ""
		Exit Function
	End If

	GetRSSURL = aTopic.Attributes("http://schemas.mindjet.com/MindManager/GSMP/2003").GetAttributeValue("RssUrl")
End Function

' Get the image location of the primary selection
' This will return TextRightImageLeft (default), TextLeftImageRight, TextTopImageBottom, or TextBottomImageTop
Function GetImageLocation(aTopic)
	If (aTopic Is Nothing) Then
		GetImageLocation = "TextRightImageLeft"
		Exit Function
	End If

    If Not(aTopic.HasAttributesNamespace("http://schemas.mindjet.com/MindManager/GSMP/2003")) Then
		GetImageLocation = "TextRightImageLeft"
		Exit Function
	End If

	Dim imageLocation As String

	imageLocation = aTopic.Attributes("http://schemas.mindjet.com/MindManager/GSMP/2003").GetAttributeValue("ImageLocation")

	If imageLocation ="" Then
		imageLocation = "TextRightImageLeft"
	End If

	GetImageLocation = imageLocation
End Function



' Download a temporary image
Function DownloadImage(url)

	If url = "" Then
		DownloadImage = ""
		Exit Function
	End If
	Dim fso

	Set fso = CreateObject("Scripting.FileSystemObject")

	Dim ext As String

	ext = fso.GetExtensionName(url)

	Dim localfile As String

	Dim wshell

	' If the next line fails, your installation probably has a corrupted Windows Scripting Host.  
	' Try downloading and installing the latest Microsoft Windows Script from
	' http://msdn.microsoft.com/library/default.asp?url=/downloads/list/webdev.asp
	Set wshell = CreateObject("WScript.Shell")

	localfile = wshell.ExpandEnvironmentStrings("%TEMP%\image.") + ext

	URLDownloadToFile(0, url, localfile, 0, 0)

	DownloadImage = localfile

End Function

' Transform the xml document with the xsl document and an image location parameter
Function Transform(xmldoc, xsldoc, imageLocation)

	Dim template As Object

	Set template=CreateObject("Msxml2.XSLTemplate.6.0")

	template.stylesheet = xsldoc.documentElement

	Dim processor As Object

	Set processor = template.createProcessor()

	processor.Input = xmldoc

	Dim Loc

	Loc = imageLocation

	processor.AddParameter("imageLocation", Loc)

	processor.transform()

	Transform = processor.output
End Function
